plugins {
    id "groovy"
    id "java-gradle-plugin"
    id "maven-publish"
    id 'org.ajoberstar.grgit' version '2.1.1'
    id 'org.ajoberstar.reckon' version '0.6.0-rc.1'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile "org.ajoberstar:grgit:2.1.1"
    compile "org.ajoberstar.reckon:reckon-gradle:0.6.0-rc.1"
    compile "org.hidetake:gradle-ssh-plugin:2.8.0"
    compile "com.eriwen:gradle-js-plugin:2.14.1"
}

gradlePlugin {
    plugins {
        FimsPlugin {
            id = "org.biocode.fims"
            implementationClass = "org.biocode.gradle.fims.FimsPlugin"
        }
        FimsAppPlugin {
            id = "org.biocode.fims-app"
            implementationClass = "org.biocode.gradle.app.FimsAppPlugin"
        }
        FimsWebAppPlugin {
            id = "org.biocode.fims-web"
            implementationClass = "org.biocode.gradle.web.FimsWebAppPlugin"
        }
    }
}

group = "org.biocode"

publishing {
    repositories {
        maven {
            url "http://repo.biocodellc.com/repository/maven-${project.version.toString().contains('dev') ? 'dev-releases' : 'releases'}"
            credentials {
                username mavenUser
                password mavenPass
            }
        }
    }
}

// ex. ./gradlew reckonTagCreate -Preckon.scope=minor -Preckon.stage=final
reckon {
    normal = scopeFromProp()
    preRelease = stageFromProp('beta', 'rc', 'final')
}

task release() {
    dependsOn build
    dependsOn reckonTagCreate
    dependsOn reckonTagPush
    dependsOn publish
}
